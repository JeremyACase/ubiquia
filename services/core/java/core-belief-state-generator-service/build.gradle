plugins {
    id 'org.springframework.boot' version "${SPRING_BOOT_VERSION}"
    id 'java'
}

bootJar {
    mainClass = 'org.ubiquia.core.belief.state.generator.Application'
    archiveFileName = 'ubiquia-core-belief-state-generator-service.jar'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${SPRING_BOOT_VERSION}"
    }
}

jar {
    enabled = false
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-loader-tools"

    // Import the Page implementation stuff without having to have a database
    implementation "org.springframework.data:spring-data-commons"

    // Adding these for now to prevent failures occurring when generating OpenAPI files from yaml
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.1'

    implementation project(':common:java:model:ubiquia')
    implementation project(':common:java:library:advice')
    implementation project(':common:java:library:api')
    implementation project(':common:java:library:implementation')

    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${JACKSON_VERSION}"
    implementation "io.kubernetes:client-java:${KUBECTL_CLIENT_VERSION}"
    implementation "io.kubernetes:client-java-extended:${KUBECTL_CLIENT_VERSION}"
    implementation "org.openapitools:openapi-generator:${OPENAPI_GENERATOR_VERSION}"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${SPRING_DOC_VERSION}"

    runtimeOnly "com.h2database:h2:${H2_VERSION}"
    runtimeOnly group: 'com.yugabyte', name: 'jdbc-yugabytedb', version:  "${YUGABYTE_DRIVER_VERSION}"
    runtimeOnly "io.micrometer:micrometer-registry-prometheus:${MICROMETER_VERSION}"

    testImplementation "org.instancio:instancio-junit:${INSTANCIO_VERSION}"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
}

/**
 * Instruct our code coverage tool where to place the report.
 */
jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
        xml.destination file("./build/reports/jacoco.xml")
        html.destination file("./build/reports/jacocoHtml")
    }
}

test {
    useJUnitPlatform()
}

def openJdkVersion = rootProject.findProperty("OPENJDK_VERSION") ?: "21"

tasks.named("processResources") {
    inputs.property("OPENJDK_VERSION", openJdkVersion)
    filteringCharset = 'UTF-8'

    // Inject our openJDK version into our yaml file.
    filesMatching("application.yaml") {
        println "✅ Forcing expand for main: $path"
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [
                OPENJDK_VERSION: openJdkVersion
        ])
    }
}


tasks.named("processTestResources") {
    inputs.property("OPENJDK_VERSION", openJdkVersion)
    filteringCharset = 'UTF-8'

    filesMatching('application.yaml') {
        println "✅ Forcing expand for test: $path"
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [
                OPENJDK_VERSION: openJdkVersion
        ])
    }
}

ext.blacklistedModules = [
    [group: 'org.slf4j', name: 'slf4j-simple'],
    [group: 'commons-logging', name: 'commons-logging'],
]

def filesExcludingModules = { Project p ->
    def cfg = p.configurations.runtimeClasspath
    def bannedIds = blacklistedModules.collect { "${it.group}:${it.name}" } as Set

    // map resolved components to their artifact files
    def bannedComponents = cfg.incoming.resolutionResult.allComponents.findAll { c ->
        c.moduleVersion != null && bannedIds.contains("${c.moduleVersion.group}:${c.moduleVersion.name}")
    }.collect { it.id }.toSet()

    def bannedFiles = cfg.incoming.artifactView { }.artifacts.artifacts.findAll { a ->
        a.id.componentIdentifier in bannedComponents
    }.collect { it.file }.toSet()

    cfg.filter { !bannedFiles.contains(it) }
}

task copyRuntimeLibs(type: Copy) {
    description = 'Copies all runtime dependencies and project jars to build/belief-state-libs'

    from(filesExcludingModules(project))

    def projectJars = [
        ':common:java:model:domain',
        ':common:java:model:ubiquia',
        ':common:java:library:advice',
        ':common:java:library:api',
        ':common:java:library:belief-state-libraries',
        ':common:java:library:dao',
        ':common:java:library:implementation'
    ]

    projectJars.each { path ->
        def subproject = project(path)

        def jarTask = subproject.tasks.named('jar')
        dependsOn(jarTask)
        from(jarTask.map { it.archiveFile.get().asFile })

        dependsOn(subproject.configurations.runtimeClasspath)
        from(filesExcludingModules(subproject))
    }

    into "belief-state-libs"
}

tasks.named("build") {
    dependsOn tasks.named("copyRuntimeLibs")
    dependsOn tasks.named("processResources")
    mustRunAfter tasks.named("copyRuntimeLibs")
    mustRunAfter tasks.named("processResources")
}

tasks.named("test") {
    dependsOn tasks.named("copyRuntimeLibs")
    dependsOn tasks.named("processTestResources")
    mustRunAfter tasks.named("copyRuntimeLibs")
    mustRunAfter tasks.named("processTestResources")
}

build.dependsOn jacocoTestReport