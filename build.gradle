buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${SPRING_BOOT_VERSION}"
    }
}

plugins {
    id 'org.springframework.boot' version "${SPRING_BOOT_VERSION}" apply false
    id 'io.spring.dependency-management' version '1.1.4' apply false
}

allprojects {
    group = 'org.ubiquia'
    version = '0.7.0'

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'
}

subprojects {
    configurations.all {
        exclude group: 'commons-logging', module: 'commons-logging'

        // Old Swagger is breaking stuff.
        resolutionStrategy.eachDependency {
            if (requested.group == "io.swagger" && requested.name == "swagger-annotations") {
                useTarget("io.swagger.core.v3:swagger-annotations-jakarta:2.2.30")
            }
        }
    }

    dependencies {
        testImplementation "org.springframework.boot:spring-boot-starter-test"
        testImplementation "org.junit.jupiter:junit-jupiter-api:${JUNIT_JUPITER_VERSION}"
        testImplementation "org.junit.jupiter:junit-jupiter-engine:${JUNIT_JUPITER_VERSION}"
        testRuntimeOnly "org.junit.platform:junit-platform-launcher:${JUNIT_PLATFORM_VERSION}"
        testRuntimeOnly "org.junit.platform:junit-platform-commons:${JUNIT_PLATFORM_VERSION}"
    }
}

checkstyle {
    configFile file("${rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '10.23.1'
}

jacoco {
    toolVersion = '0.8.8'
}

