plugins {
    id 'org.springframework.boot' version "${SPRING_BOOT_VERSION}"
    id 'java'
}

bootJar {
    mainClass = 'org.ubiquia.core.belief.state.generator.Application'
    archiveFileName = 'ubiquia-core-belief-state-generator-service.jar'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${SPRING_BOOT_VERSION}"
    }
}

jar {
    enabled = false
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-loader-tools"

    // Adding these for now to prevent failures occurring when generating OpenAPI files from yaml
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.1'

    implementation project(':common:model:ubiquia')
    implementation project(':common:library:advice')
    implementation project(':common:library:config')

    implementation "ch.qos.logback:logback-core:${LOGBACK_VERSION}"
    implementation "io.kubernetes:client-java:${KUBECTL_CLIENT_VERSION}"
    implementation "io.kubernetes:client-java-extended:${KUBECTL_CLIENT_VERSION}"
    implementation "org.openapitools:openapi-generator:${OPENAPI_GENERATOR_VERSION}"

    runtimeOnly "io.micrometer:micrometer-registry-prometheus:${MICROMETER_VERSION}"

    testImplementation "org.instancio:instancio-junit:${INSTANCIO_VERSION}"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
}


/**
 * Instruct our code coverage tool where to place the report.
 */
jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
        xml.destination file("./build/reports/jacoco.xml")
        html.destination file("./build/reports/jacocoHtml")
    }
}

test {
    useJUnitPlatform()
}


task copyRuntimeLibs(type: Copy) {
    description = 'Copies all runtime dependencies and project jars to build/belief-state-libs'

    from configurations.runtimeClasspath

    def projectJars = [
        ':common:model:acl',
        ':common:model:ubiquia',
        ':common:library:advice',
        ':common:library:api',
        ':common:library:belief-state-libraries',
        ':common:library:config',
        ':common:library:dao',
        ':common:library:logic'
    ]

    projectJars.each { path ->
        def subproject = project(path)

        // Add the compiled jar
        def jarTask = subproject.tasks.named('jar')
        dependsOn(jarTask)
        from(jarTask.map { it.archiveFile.get().asFile })

        // Add runtime dependencies from the subproject
        dependsOn(subproject.configurations.runtimeClasspath)
        from(subproject.configurations.runtimeClasspath)
    }

    into "belief-state-libs"
}

build.mustRunAfter copyRuntimeLibs
build.dependsOn copyRuntimeLibs
